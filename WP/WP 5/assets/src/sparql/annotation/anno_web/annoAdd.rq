PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

#import Text files

CONSTRUCT {
    ?s_anno 
        a kfngoeo:AnnoAdd ;
        rdfs:label "Annotation level of Add"@en ;                
        kfngoeo:hasTarget [
            kfngoeo:hasSource ?title_add ;
            kfngoeo:hasSelector [
                a kfngoeo:TextPositionSelector ;
                kfngoeo:start ?start_nr ;
                kfngoeo:end ?end_nr ;
            ] 
        ] ;
        #no annotation body               
    .
}
WHERE {
    ?s_tei 
        a kfngoeo:StartTag ;
        kfngoeo:elementName tei:TEI ;
        kfngoeo:elementPos ?o_pos_tei ;
	.
    BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
    ?s_add_start 
        a kfngoeo:StartTag ;
        kfngoeo:elementName tei:add ;
        kfngoeo:hasAttr [            
            kfngoeo:attrName "xml:id" ;
            kfngoeo:attrValue ?o_id_add ;
        ] ;                    
        kfngoeo:elementPos ?o_pos_add_start ;
	.
    FILTER(CONTAINS(?o_pos_add_start, ?title_short))
    ?s_add_end 
        a kfngoeo:EndTag ;
        kfngoeo:elementName tei:add ;
        kfngoeo:hasAttr [            
            kfngoeo:attrName "xml:id" ;
            kfngoeo:attrValue ?o_id_add ;
        ] ;                    
        kfngoeo:elementPos ?o_pos_add_end ;
	.
    BIND(xsd:integer(SUBSTR(?o_pos_add_start,strlen(?title_short)+2)) AS ?start_nr)
    BIND(xsd:integer(SUBSTR(?o_pos_add_end,strlen(?title_short)+2)) AS ?end_nr)
    BIND(?title_short AS ?title_add)    
    OPTIONAL {
        BIND (IF(BOUND(?s_add_start), URI(CONCAT("https://github.com/KfNGOe/kfngoei/", STRUUID())), ?nothing) AS ?s_anno)
    }
}