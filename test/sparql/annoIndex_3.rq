#build Annotation level of Index
#build targets of annotations

PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace/>

#import: TB or MF, annoIndexi_2

CONSTRUCT {
    ?s_anno a kfngoeo:AnnoIndex ;
        rdfs:label "Annotation level of Index"@en ;
    	rdfs:isDefinedBy ?o_key ;
        kfngoeo:hasTarget ?_bn1 ;
        kfngoeo:hasBody kfngoei:nnnnnn ;
	.
    ?_bn1 
    	kfngoeo:hasSource ?title_index ;
		kfngoeo:hasSelector ?_bn ;
    .
    ?_bn 
    	a kfngoeo:TextPositionSelector ;
        kfngoeo:start ?start_nr ;
        kfngoeo:end ?end_nr ; 
    .
}
WHERE {
    {
        SELECT DISTINCT ?o_key_text (?title_short AS ?title_index) (IF(BOUND(?title_index), BNODE(), ?nothing) AS ?_bn1) ?start_nr ?end_nr (IF(BOUND(?start_nr), BNODE(), ?nothing) AS ?_bn)
        WHERE {            
                ?s_tei a kfngoeo:StartTag ;
                         kfngoeo:elementName tei:TEI ;
                         kfngoeo:elementPos ?o_pos_tei ;
                .
                BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
                ?s_text a kfngoeo:StartTag ;
                    kfngoeo:elementName tei:term ;                    
                    kfngoeo:hasAttr [
                        kfngoeo:attrName "key" ;
                        kfngoeo:attrValue ?o_key_text ;                    
                    ] ;
                    kfngoeo:elementPos ?o_pos_text ;
                .
            	FILTER(CONTAINS(?o_pos_text, ?title_short))            
                BIND(xsd:integer(substr(?o_pos_text,strlen(?title_short)+2)) AS ?o_pos_text_nr)
                BIND(concat(?title_short, '_', str(?o_pos_text_nr-1)) AS ?o_pos_index_start)
                ?s_index a kfngoeo:StartTag ;
                         kfngoeo:elementName tei:index ;
                         kfngoeo:hasAttr [            
                            kfngoeo:attrName "xml:id" ;
                            kfngoeo:attrValue ?o_id_index ;
                        ] ;
                         kfngoeo:elementPos ?o_pos_index_start ;
				.
                BIND(xsd:integer(substr(?o_pos_index_start,strlen(?title_short)+2)) AS ?start_nr)                
                ?s_index_end a kfngoeo:EndTag ;
                         kfngoeo:elementName tei:index ;
                         kfngoeo:hasAttr [            
                            kfngoeo:attrName "xml:id" ;
                            kfngoeo:attrValue ?o_id_index ;
                        ] ;
                         kfngoeo:elementPos ?o_pos_index_end ;
				.
                BIND(xsd:integer(substr(?o_pos_index_end,strlen(?title_short)+2)) AS ?end_nr)            
        }
	}
    ?s_anno a kfngoeo:AnnoIndex ;
            rdfs:isDefinedBy ?o_key ;            
    .    
    FILTER (?o_key = ?o_key_text)    
} ORDER BY ?o_key