PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>

#import annoPersoni21 + register_person

CONSTRUCT {
    ?s_anno 	
        a kfngoeo:AnnoPerson;
  		rdfs:label "Annotation level of Person"@en;
  		rdfs:isDefinedBy ?key;
    	kfngoeo:hasTarget [
            kfngoeo:hasSource ?title_short ;
            kfngoeo:hasSelector [
                a kfngoeo:TextPositionSelector ;
                kfngoeo:start ?start_nr ;
            	kfngoeo:end ?end_nr ;
             ]
    	] ;
  		kfngoeo:hasBody ?body_person 
    .   
}
WHERE {
    ?s_anno 	
        a kfngoeo:AnnoPerson;
  		rdfs:label "Annotation level of Person"@en;
  		rdfs:isDefinedBy ?key;
    	kfngoeo:hasTarget [
            kfngoeo:hasSource ?title_short ;
            kfngoeo:hasSelector [
                a kfngoeo:TextPositionSelector ;
                kfngoeo:start ?start_nr ;
            	kfngoeo:end ?end_nr ;
             ]
    	] ;  		
    .
    {
        SELECT ?s_item ?o_key_txt
        WHERE {
            ?s_tei 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:TEI ;
                kfngoeo:elementPos ?o_pos_tei ;
            .            
            BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
            ?s_item
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:item ;        
                kfngoeo:elementPos ?o_pos_item ;
            .
            FILTER(CONTAINS(?o_pos_item, ?title_short))
            BIND(xsd:integer(substr(?o_pos_item,strlen(?title_short)+2)) AS ?o_pos_item_nr)
            BIND(concat(?title_short, '_', str(?o_pos_item_nr + 2)) AS ?o_pos_txt_nr)
            ?s_txt 
				a kfngoeo:Text ;
				kfngoeo:hasContent ?o_key_txt ;
				kfngoeo:elementPos ?o_pos_txt_nr ;
 			.
        }
    }
    FILTER(?key = ?o_key_txt)
    BIND(?s_item AS ?body_person)
}