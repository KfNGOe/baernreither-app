PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace/>

#import Text files + register index

CONSTRUCT {
    ?s_anno 	
        a kfngoeo:AnnoIndexSub ;
		rdfs:label "Annotation level of Index Sub terms"@en ;
		rdfs:isDefinedBy ?key ;        
        kfngoeo:hasTarget [
            kfngoeo:hasSource ?title_target ;
            kfngoeo:hasSelector [
                a kfngoeo:TextPositionSelector ;
                kfngoeo:start ?start_nr_target ;
            	kfngoeo:end ?end_nr_target ;
            ] ;
            kfngoeo:hasSelector ?s_index_start ;  
        ] ;
    .
    #no annotation body    
}
WHERE {
    {
        SELECT DISTINCT ?o_key_txt
        WHERE {
            ?s_tei 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:TEI ;
                kfngoeo:elementPos ?o_pos_tei ;
                .            
            BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
            ?s_term_start 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:term ;
                kfngoeo:hasAttr [
                    kfngoeo:attrName "key" ;
                    kfngoeo:attrValue ?o_key_txt ;
                ] ;        
                kfngoeo:elementPos ?o_pos_term_start ;
            .
            FILTER(CONTAINS(?o_pos_term_start, ?title_short))
        }
    }
    OPTIONAL {
        BIND (IF(BOUND(?o_key_txt), URI(CONCAT("https://github.com/KfNGOe/kfngoei/", STRUUID())), ?nothing) AS ?s_anno)
        #BIND(IF(BOUND(?s_anno), BNODE(), ?nothing) AS ?_bn_body)
    }
    {
        SELECT ?s_index_start ?o_key ?o_pos_start_nr ?o_pos_end_nr ?title_target
        WHERE {
            ?s_tei_1 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:TEI ;
                kfngoeo:elementPos ?o_pos_tei_1 ;
            .
            BIND(substr(?o_pos_tei_1,1,strlen(?o_pos_tei_1)-2) AS ?title_short)
            ?s_text 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:text ;
                kfngoeo:elementPos ?o_pos_text ;
            .
            FILTER(CONTAINS(?o_pos_text, ?title_short))    
            ?s_term_end 
                a kfngoeo:EndTag ;
                kfngoeo:elementName tei:term ;		
                kfngoeo:hasAttr [
                    kfngoeo:attrName "xml:id" ;
                    kfngoeo:attrValue ?xmlId_term;
                ] ;
                kfngoeo:elementPos ?o_pos_term ;    
            .
            FILTER(CONTAINS(?o_pos_term, ?title_short))
            BIND(xsd:integer(substr(?o_pos_term,strlen(?title_short)+2)) AS ?o_pos_term_nr)
            BIND(concat(?title_short, '_', str(?o_pos_term_nr+1)) AS ?o_pos_start)
            ?s_index_start
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:index ;	
                kfngoeo:hasAttr [
                    kfngoeo:attrName "xml:id" ;
                    kfngoeo:attrValue ?xmlId;
                ] ;
                kfngoeo:elementPos ?o_pos_start ;
            .
            BIND(xsd:integer(substr(?o_pos_start,strlen(?title_short)+2)) AS ?o_pos_start_nr)
            BIND(concat(?title_short, '_', str(?o_pos_start_nr+1)) AS ?o_pos_term_start_1)
            ?s_term_start_1 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:term ;
                kfngoeo:hasAttr [
                    kfngoeo:attrName "key" ;
                    kfngoeo:attrValue ?o_key ;
                ] ;        
                kfngoeo:elementPos ?o_pos_term_start_1 ;
            .
            ?s_index_end 
                a kfngoeo:EndTag ;
                kfngoeo:elementName tei:index ;		
                kfngoeo:hasAttr [
                    kfngoeo:attrName "xml:id" ;
                    kfngoeo:attrValue ?xmlId;
                ] ;
                kfngoeo:elementPos ?o_pos_end ;    
            .
            BIND(xsd:integer(substr(?o_pos_text,strlen(?title_short)+2)) AS ?o_pos_text_nr)        
            FILTER(?o_pos_start_nr > ?o_pos_text_nr)
            BIND(xsd:integer(substr(?o_pos_end,strlen(?title_short)+2)) AS ?o_pos_end_nr)            
            BIND(?title_short AS ?title_target)
        }
    }
    FILTER (?o_key = ?o_key_txt)
    BIND(?o_key AS ?key)
    BIND(?o_pos_start_nr AS ?start_nr_target)
    BIND(?o_pos_end_nr AS ?end_nr_target)
} ORDER BY ?o_key 