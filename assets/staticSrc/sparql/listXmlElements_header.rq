#List all distinct elements of tei:header in a ttl file

PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

#import: TB or MF files

SELECT DISTINCT ?o_name
WHERE {
    ?s_tei a kfngoeo:StartTag ;
        kfngoeo:elementName tei:TEI ;
        kfngoeo:elementPos ?o_pos_tei ;
	.
    BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
    ?s_header 
        a kfngoeo:StartTag ;
        kfngoeo:elementName tei:teiHeader ;
        kfngoeo:elementPos ?o_pos_header ;
	.
    BIND(xsd:integer(substr(?o_pos_header,strlen(?title_short)+2)) AS ?o_pos_header_nr)
    FILTER(CONTAINS(?o_pos_header, ?title_short))    
    ?s_text 
        a kfngoeo:StartTag ;
        kfngoeo:elementName tei:text ;
        kfngoeo:elementPos ?o_pos_text ;
	.
    BIND(xsd:integer(substr(?o_pos_text,strlen(?title_short)+2)) AS ?o_pos_text_nr)
    FILTER(CONTAINS(?o_pos_text, ?title_short))    
	?s_name 
        a kfngoeo:StartTag ;
        kfngoeo:elementName ?o_name ; 
        kfngoeo:elementPos ?o_pos_name ;
    .
    BIND(xsd:integer(substr(?o_pos_name,strlen(?title_short)+2)) AS ?o_pos_name_nr)
    FILTER(CONTAINS(?o_pos_name, ?title_short))    
    FILTER(?o_pos_header_nr < ?o_pos_name_nr && ?o_pos_name_nr < ?o_pos_text_nr)
} ORDER BY ?o_name