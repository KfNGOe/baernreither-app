#build Annotation level of Index
#build bodies of annotations

PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

#import: annoIndexi_3, register_index

CONSTRUCT {
    ?s_anno a kfngoeo:AnnoIndex ;
        rdfs:label "Annotation level of Index"@en ;
    	rdfs:isDefinedBy ?o_key ;
        kfngoeo:hasTarget ?target_text ;
        kfngoeo:hasBody ?body_index ;
	.
}
WHERE {
    ?s_anno a kfngoeo:AnnoIndex ;
        rdfs:isDefinedBy ?o_key ;
        kfngoeo:hasTarget ?target_text ;
        kfngoeo:hasBody ?body ;
    .
	{
        SELECT DISTINCT ?o_text (?s_item AS ?body_index)
        WHERE {
            OPTIONAL {
                ?s_tei a kfngoeo:StartTag ;
                         kfngoeo:elementName tei:TEI ;
                         kfngoeo:elementPos ?o_pos_tei ;
                .
                BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
                ?s_text a kfngoeo:Text ;
                        kfngoeo:hasContent ?o_text ;
                        kfngoeo:elementPos ?o_pos_text ;
                .
                BIND(xsd:decimal(substr(?o_pos_text,strlen(?title_short)+2)) AS ?o_pos_text_nr)
                BIND(concat(?title_short, '_', str(?o_pos_text_nr-2)) AS ?o_pos_item)
                ?s_item a kfngoeo:StartTag ;
                        kfngoeo:elementName tei:item ;
                        kfngoeo:elementPos ?o_pos_item ;
                .
            }
        }
	}    
    FILTER (?o_key = ?o_text)
}