#build Annotation level of Text Comparison
#generate pseudo resource IRI, start and end position of target

PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    ?s_app
        a kfngoeo:AnnoTextComparison ;
        rdfs:label "Annotation level of text comparison"@en ;
        kfngoeo:hasTarget [
            kfngoeo:hasSource "nnnn" ;
            kfngoeo:hasSelector [
                a kfngoeo:TextPositionSelector ;
                kfngoeo:start ?o_start_nr ;
                kfngoeo:end ?o_end_nr ;
            ] 
        ] ;    
        kfngoeo:hasBody [        
            a kfngoeo:TextualBody;
            kfngoeo:hasSource <https://nnnn> ;
            rdf:value "nnnnn"@de ;        
            rdfs:isDefinedBy "nnnn" ;
        ] ;         
    .
}
{
    SELECT ?s_app (MIN(?o_pos_nr_subtr) AS ?o_min) (?o_pos_app_nr - ?o_min AS ?o_start_nr) (?o_pos_app_end_nr AS ?o_end_nr)
    WHERE {
        #get element position of tei:TEI
        ?s_tei a kfngoeo:StartTag ;
                kfngoeo:elementName tei:TEI ;
                kfngoeo:elementPos ?o_pos_tei ;
        .        
        #get short title from element position of tei:TEI
        BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-1) AS ?title_short)
        ?s_app a kfngoeo:StartTag ;
            kfngoeo:elementName tei:app ;
            kfngoeo:hasAttr [		
                kfngoeo:attrValue ?xmlid ;
            ] ;
            kfngoeo:elementPos ?o_pos_app ;
        .
        ?s_app_end a kfngoeo:EndTag ;
            kfngoeo:elementName tei:app ;
            kfngoeo:hasAttr [		
                kfngoeo:attrValue ?xmlid ;
            ] ;
            kfngoeo:elementPos ?o_pos_app_end ;
        .
        BIND(xsd:decimal(substr(?o_pos_app,strlen(?title_short)+1)) AS ?o_pos_app_nr)
        BIND(xsd:decimal(substr(?o_pos_app_end,strlen(?title_short)+1)) AS ?o_pos_app_end_nr)
        ?s_anchor a kfngoeo:StartTag ;
            kfngoeo:elementName tei:anchor ;
            kfngoeo:elementPos ?o_pos_anchor ;
        .
        BIND(xsd:decimal(substr(?o_pos_anchor,strlen(?title_short)+1)) AS ?o_pos_anchor_nr)
        BIND( (?o_pos_app_nr-?o_pos_anchor_nr) AS ?o_pos_nr_subtr)
        FILTER (?o_pos_nr_subtr > 0)        
    } 
    GROUP BY ?s_app ?o_pos_app_nr ?o_pos_app_end_nr    
}