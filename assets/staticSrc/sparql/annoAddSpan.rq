PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace/>

#import: Text files

CONSTRUCT {
    ?s_anno
        a kfngoeo:AnnoAddSpan ;
        rdfs:label "Annotation level of addSpan"@en ;
        kfngoeo:hasTarget [
            kfngoeo:hasSource ?title_target ;
            kfngoeo:hasSelector [
                a kfngoeo:TextPositionSelector ;
                kfngoeo:start ?start_nr ;
                kfngoeo:end ?end_nr ;
            ] 
        ] ;
        kfngoeo:hasBody [        
        	a kfngoeo:TextualBody ;
         	rdf:value ?span_type ;
        ] ;        
    .
}
WHERE {
    ?s_tei a kfngoeo:StartTag ;
           kfngoeo:elementName tei:TEI ;
           kfngoeo:elementPos ?o_pos_tei ;
    .
    BIND(SUBSTR(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
    BIND(?title_short AS ?title_target)
    ?s_addsp a kfngoeo:StartTag ;
            kfngoeo:elementName tei:addSpan ;
            kfngoeo:hasAttr [
                kfngoeo:attrName "spanTo" ;
                kfngoeo:attrValue ?o_span_addsp ;
            ] ;
            kfngoeo:hasAttr [
				kfngoeo:attrName "type" ;
				kfngoeo:attrValue ?o_type_addsp ;
			] ;            
            kfngoeo:elementPos ?o_pos_addsp ;
    .
    FILTER(CONTAINS(?o_pos_addsp, ?title_short))    
    BIND(STRAFTER(?o_span_addsp,'#') AS ?o_span)
    BIND(xsd:integer(SUBSTR(?o_pos_addsp,strlen(?title_short)+2)) AS ?start_nr)    
    ?s_anchor a kfngoeo:StartTag ;
              kfngoeo:elementName tei:anchor ;
              kfngoeo:hasAttr [
				kfngoeo:attrName "xml:id" ;
				kfngoeo:attrValue ?o_span ;
			] ;
   			kfngoeo:elementPos ?o_pos_anch ;
	.
    BIND(xsd:integer(SUBSTR(?o_pos_anch,strlen(?title_short)+2)) AS ?end_nr)
    BIND(?o_type_addsp AS ?span_type)
    OPTIONAL {
        BIND (IF(BOUND(?s_addsp), URI(CONCAT("https://github.com/KfNGOe/kfngoei/", STRUUID())), ?nothing) AS ?s_anno)
    }
}