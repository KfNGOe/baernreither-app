PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT{
	?s_anno 
        a kfngoeo:AnnoTextComparison ;
        ?p_anno ?o_anno ;
        kfngoeo:hasTarget ?_target ;
        kfngoeo:hasBody ?_body ;        
    .
    ?_target ?p_target ?o_target ;
             kfngoeo:hasSelector ?_sel ;
	.
    ?_sel ?p_sel ?o_sel ;
    .
    ?_body kfngoeo:hasSource ?from ;
           ?p_body ?o_bodys ;
    .
}
WHERE {
    ?s_anno a kfngoeo:AnnoTextComparison ;
            kfngoeo:hasTarget ?_target ;
            kfngoeo:hasBody ?_body ;        
            ?p_anno ?o_anno ;            
	.
    ?_target ?p_target ?o_target ;
             kfngoeo:hasSelector ?_sel ;
	.
    ?_sel ?p_sel ?o_sel ;
    .
    ?_body ?p_body ?o_body ;
    .
    BIND(IF(?p_body = kfngoeo:hasSource, ?nothing, ?o_body) AS ?o_bodys
                        )
    FILTER(?p_body != rdfs:seeAlso)
    {
        SELECT ?s_anno (?s_anno_comp AS ?from)
        WHERE {    
            ?s_tei a kfngoeo:StartTag ;
                   kfngoeo:elementName tei:TEI ;
                   kfngoeo:elementPos ?o_pos_tei ;
            .
            ?s_anno a kfngoeo:AnnoTextComparison ;
                kfngoeo:hasTarget [
                    kfngoeo:hasSource ?o_source_target ;
            ] ;
                kfngoeo:hasBody [
                    a kfngoeo:TextualBody ;
                    kfngoeo:hasSource ?o_source_body ;
                    rdfs:isDefinedBy ?o_status ;
            ]
            .
            ?s_source_body a kfngoeo:StartTag ;
                kfngoeo:elementName tei:anchor ;
                kfngoeo:hasAttr [
                    kfngoeo:attrName "xml:id" ;
                    kfngoeo:attrValue ?o_source_body ;
            ] ;
                kfngoeo:elementPos ?o_source_body_pos ;
            .
            BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
            FILTER(CONTAINS(?o_source_body_pos, ?title_short))
            BIND(xsd:integer(substr(?o_source_body_pos,strlen(?title_short)+2)) AS ?o_source_body_pos_nr)
            ?s_anno_comp a kfngoeo:AnnoTextComparison ;  		
                        kfngoeo:hasTarget [
                        kfngoeo:hasSource ?o_source_comp ;
                        kfngoeo:hasSelector [
                            a kfngoeo:TextPositionSelector ;
                            kfngoeo:start ?o_source_body_pos_nr ;
                ]
            ]	 
            .    
            FILTER(str(?o_source_comp) = str(?title_short))            
        }
    }
}   