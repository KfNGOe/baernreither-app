PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

#import TB or MF

CONSTRUCT {    
    ?s_anno
        a kfngoeo:AnnoApp ;
        rdfs:label "Annotation level of App"@en ;
        kfngoeo:hasTarget [
            kfngoeo:hasSource ?title_app ;
            kfngoeo:hasSelector [
                a kfngoeo:TextPositionSelector ;
                kfngoeo:start ?start_nr ;
                kfngoeo:end ?end_nr ;
            ] 
        ] ;
        #no annotation body               
    .
}
WHERE {    
    ?s_app_start a kfngoeo:StartTag ;
        kfngoeo:elementName tei:app ;
        kfngoeo:hasAttr [            
            kfngoeo:attrName "xml:id" ;
            kfngoeo:attrValue ?o_id_app ;
        ] ;                    
        kfngoeo:elementPos ?o_pos_app_start ;
	.
    ?s_app_end a kfngoeo:EndTag ;
        kfngoeo:elementName tei:app ;
        kfngoeo:hasAttr [            
            kfngoeo:attrName "xml:id" ;
            kfngoeo:attrValue ?o_id_app ;
        ] ;                    
        kfngoeo:elementPos ?o_pos_app_end ;
	.
    ?s_tei a kfngoeo:StartTag ;
        kfngoeo:elementName tei:TEI ;
        kfngoeo:elementPos ?o_pos_tei ;
	.
    BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
    FILTER(CONTAINS(?o_pos_app_start, ?title_short))
    BIND(xsd:integer(SUBSTR(?o_pos_app_start,strlen(?title_short)+2)) AS ?start_nr)
    BIND(xsd:integer(SUBSTR(?o_pos_app_end,strlen(?title_short)+2)) AS ?end_nr)
    BIND(?title_short AS ?title_app)
    BIND(IF(BOUND(?s_app_start), URI(CONCAT("https://github.com/KfNGOe/kfngoei/", STRUUID())), ?nothing) AS ?s_anno)
}