PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace/>

#import: Text files

CONSTRUCT {
    ?s_anno
        a kfngoeo:AnnoTextComparison ;
        rdfs:label "Annotation level of text comparison"@en ;
        kfngoeo:hasTarget [
            kfngoeo:hasSource ?title_target ;
            kfngoeo:hasSelector [
                a kfngoeo:TextPositionSelector ;
                kfngoeo:start ?start_nr_target ;
                kfngoeo:end ?end_nr_target ;
            ] 
        ] ;    
        kfngoeo:hasBody [        
            a kfngoeo:TextualBody;
            kfngoeo:hasSource ?title_body ; 
            kfngoeo:hasSelector ?start_nr_body ;	#start nr of body            
            rdfs:isDefinedBy ?comp_status ;
        ] ;         
    .
}
WHERE {    
    {
      SELECT ?s_app_start (MIN(?o_pos_nr_subtr) AS ?o_min) (?o_pos_app_start_nr - ?o_min AS ?start_nr_target) 
        	(?o_pos_app_end_nr AS ?end_nr_target) (substr(?title_short,1,strlen(?title_short)-1) AS ?title_target)
        WHERE {
            #get element position of tei:TEI
            ?s_tei 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:TEI ;
                kfngoeo:elementPos ?o_pos_tei ;
            .        
            #get short title from element position of tei:TEI
            BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-1) AS ?title_short)            
            ?s_app_start 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:app ;
                kfngoeo:hasAttr [		
                    kfngoeo:attrValue ?xmlid ;
                ] ;
                kfngoeo:elementPos ?o_pos_app_start ;
            .
            FILTER(CONTAINS(?o_pos_app_start, ?title_short))
            ?s_app_end 
                a kfngoeo:EndTag ;
                kfngoeo:elementName tei:app ;
                kfngoeo:hasAttr [		
                    kfngoeo:attrValue ?xmlid ;
                ] ;
                kfngoeo:elementPos ?o_pos_app_end ;
            .
            BIND(xsd:integer(substr(?o_pos_app_start,strlen(?title_short)+1)) AS ?o_pos_app_start_nr)
            BIND(xsd:integer(substr(?o_pos_app_end,strlen(?title_short)+1)) AS ?o_pos_app_end_nr)            
            ?s_anchor 
                a kfngoeo:StartTag ;
                kfngoeo:elementName tei:anchor ;
                kfngoeo:elementPos ?o_pos_anchor ;
            .
    		FILTER(CONTAINS(?o_pos_anchor, ?title_short))
            BIND(xsd:integer(substr(?o_pos_anchor,strlen(?title_short)+1)) AS ?o_pos_anchor_nr)
            BIND( (?o_pos_app_start_nr-?o_pos_anchor_nr) AS ?o_pos_nr_subtr)
            FILTER (?o_pos_nr_subtr > 0)        
        } 
        GROUP BY ?s_app_start ?o_pos_app_start_nr ?o_pos_app_end_nr ?title_short      
    }
    ?s_app_start a kfngoeo:StartTag ;
                 kfngoeo:elementPos ?o_pos_app ;
                 kfngoeo:hasAttr [
        			kfngoeo:attrName "from" ;
                    kfngoeo:attrValue ?o_app_from ; #xmlId of tei:anchor
        		 ] ;
           		 kfngoeo:hasAttr [
        		 	kfngoeo:attrName "type" ;
            		kfngoeo:attrValue ?o_comp_status ;
        		 ] ;
	.
    BIND(strafter(?o_app_from,'#') AS ?o_from)
    OPTIONAL {
    	?s_anch	        
            kfngoeo:elementName tei:anchor ;
            kfngoeo:hasAttr [
                kfngoeo:attrName "xml:id" ;
                kfngoeo:attrValue ?o_from ; 
            ] ;
            kfngoeo:elementPos ?o_pos_from ;
		.
    }
    BIND(REPLACE(?o_pos_from, "(_[^_]+$)", "") AS ?title_body )
    BIND(xsd:integer(substr(?o_pos_from,strlen(?title_body)+2)) AS ?start_nr_body)
    BIND(?o_comp_status AS ?comp_status)    
    OPTIONAL {
        BIND(IF(BOUND(?s_app_start), URI(CONCAT("https://github.com/KfNGOe/kfngoei/", STRUUID())), ?nothing) AS ?s_anno)
    }
}