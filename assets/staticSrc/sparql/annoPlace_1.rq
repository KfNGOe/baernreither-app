PREFIX kfngoeo: <https://github.com/KfNGOe/kfngoeo#>
PREFIX tei: <http://www.tei-c.org/ns/1.0/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfngoei: <https://github.com/KfNGOe/kfngoei/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

#import TB or MF

CONSTRUCT {
    ?s_anno a kfngoeo:AnnoPlace ;
        	rdfs:label "Annotation level of Place"@en ;                
    		rdfs:isDefinedBy ?work_place ;        
        	kfngoeo:hasTarget ?target_place ;
         	kfngoeo:hasBody ?o_key_place
    .
}
WHERE {
    ?s_tei a kfngoeo:StartTag ;
           kfngoeo:elementName tei:TEI ;
           kfngoeo:elementPos ?o_pos_tei ;
	.
    BIND(substr(?o_pos_tei,1,strlen(?o_pos_tei)-2) AS ?title_short)
    ?s_text a kfngoeo:StartTag ;
       kfngoeo:elementName tei:text ;
       kfngoeo:elementPos ?o_pos_text ;
	.
    BIND(xsd:integer(substr(?o_pos_text,strlen(?title_short)+2)) AS ?o_pos_text_nr)
    ?s_place a kfngoeo:StartTag ;
           kfngoeo:elementName tei:placeName ;
           kfngoeo:hasAttr [
               kfngoeo:attrName "key" ;
               kfngoeo:attrValue ?o_key_place ;
            ] ;
           kfngoeo:elementPos ?o_pos_place ;
	.
    BIND(xsd:integer(substr(?o_pos_place,strlen(?title_short)+2)) AS ?o_pos_place_nr)    
    FILTER(?o_pos_place_nr > ?o_pos_text_nr)    
    BIND(?s_place AS ?target_place)
    BIND(?title_short AS ?work_place)
    BIND(IF(BOUND(?s_place), URI(CONCAT("https://github.com/KfNGOe/kfngoei/", STRUUID())), ?nothing) AS ?s_anno)
}